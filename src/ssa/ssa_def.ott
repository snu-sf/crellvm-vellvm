
% Syntax of Static Single Assignment(SSA)

embed 
{{ coq
Require Import ListSet.
Require Import Logic_Type.
Require Import monad.
Require Import Metatheory.

Module LLVMsyntax.

Section Lists.

  Variable A : Type.

  Fixpoint last_opt (l:list A) {struct l} : option A := 
  match l with 
    | nil => None 
    | a :: nil => Some a 
    | a :: l' => last_opt l'
  end.

End Lists. 
}}

metavar INT ::= {{ com a natural number }}
  {{ coq nat }} {{ lex numeral  }}
metavar id ::= {{ com identities }}
  {{ coq atom }} {{ lex numeral  }}
metavar l ::= {{ com labels }}
  {{ coq atom }} {{ lex numeral  }}
metavar align ::= {{ com align }}
  {{ coq nat }} {{ lex numeral }}
metavar sz ::= {{ com sz }}
  {{ coq nat }} {{ lex numeral }}
indexvar i, j, n ::= {{ com index }}
  {{ coq nat }}
 
grammar

opt_INT {{ tex \mathit{opt_{INT} } }} :: 'opt_INT_'	::=			{{ com option INT }} {{ coq option INT }}
  |  none 						:: M :: none		{{ com none }} {{ coq None }}
  |  INT						:: M :: some  		{{ com some }} {{ coq (Some [[INT]]) }}

opt_l {{ tex \mathit{opt_{l} } }} :: 'opt_l_'		::=			{{ com option l }} {{ coq option l }}
  |  none 						:: M :: none		{{ com none }} {{ coq None }}
  |  l							:: M :: some  		{{ com some }} {{ coq (Some [[l]]) }}

opt_id {{ tex \mathit{opt_{id} } }} :: 'opt_id_'	::=			{{ com option id }} {{ coq option id }}
  |  none 						:: M :: none		{{ com none }} {{ coq None }}
  |  id							:: M :: some  		{{ com some }} {{ coq (Some [[id]]) }}

ids {{ tex \mathit{\{id\} } }} :: 'ids_'		::=			{{ com id set }}
       	   			      	 					{{ coq (list id) }}
  | empty						::   :: empty		{{ coq (empty_set id) }}
  | id1 .. idn						::   :: set		{{ coq [[id1 .. idn]] }}
  | ids , id						::   :: cons		{{ coq (set_add eq_dec [[id]] [[ids]]) }}
  | ids ; ids'						::   :: append		{{ coq (set_union eq_dec [[ids]] [[ids']]) }}
  | ( ids )						:: S :: paren		{{ coq [[ ids ]] }}

layout :: 'layout_'					::=  			{{ com data layout }}
  | bigendian						::   :: be 		{{ com bigendian }}
  | littleendian					::   :: le	 	{{ com littleendian }}
  | ptr sz align0 align1				::   :: ptr		{{ com ptr }}
  | int sz align0 align1				::   :: int		{{ com int }}
  | aggr sz align0 align1			     	::   :: aggr		{{ com aggregate }}
  | stack sz align0 align1				::   :: stack		{{ com stack }}

list_layout {{ tex \mathit{\overline{layout} } }} :: 'list_layout_'	::=  	{{ com layout list }}
     	    			     	   					{{ coq (list layout) }}
  | []   		 				::   :: nil		{{ coq nil }}
  | list_layout , layout				::   :: cons		{{ coq ([[layout]]::[[list_layout]]) }}
  | list_layout ; list_layout'				::   :: append		{{ coq ([[list_layout]]++[[list_layout']]) }}
  | ( list_layout )					:: S :: paren           {{ coq [[ list_layout ]] }}

typ :: 'typ_'						::=			{{ com type }}
  | int sz                    				::   :: int         	{{ com integer }}
  %| float						::   :: float		{{ com float }}
  %| double						::   :: double          {{ com double }}
  %| fp128						::   :: fp128           {{ com fp128 }}
  | void                      				::   :: void        	{{ com void }}
  | label                                               ::   :: label           {{ com label }}
  | metadata                                            ::   :: metadata        {{ com metadata }}
  | [ sz * typ ]              				::   :: array       	{{ com array }}
  | typ ( </ typj // j /> )  				::   :: function    	{{ com function }}
  | { </ typj // j /> }       				::   :: struct      	{{ com structure }}
  | typ *                     				::   :: pointer     	{{ com pointer }}
  %| < sz * typ >             				::   :: vector       	{{ com vector }}
  %| opaque  						::   :: opaque          {{ com opaque }}
  % x86_fp80
  % ppc_fp128
  % union
  | ( typ )						:: S :: paren           {{ coq [[ typ ]] }}

opt_typ {{ tex \mathit{opt_{typ} } }} :: 'opt_typ_'	::=			{{ com option typ }} {{ coq option typ }}
  |  none 						:: M :: none		{{ com none }} {{ coq None }}
  |  typ						:: M :: some  		{{ com some }} {{ coq (Some [[typ]]) }}

list_typ {{ tex \mathit{\overline{typ} } }} :: 'list_typ_'	::=  		{{ com typ list }}
     	    			     	   					{{ coq (list typ) }}
  | []   		 				::   :: nil		{{ coq nil }}
  | list_typ , typ 					::   :: cons		{{ coq ([[typ]]::[[list_typ]]) }}
  | list_typ ; list_typ'				::   :: append		{{ coq ([[list_typ]]++[[list_typ']]) }}
  | ( list_typ )					:: S :: paren           {{ coq [[ list_typ ]] }}

const :: 'const_'					::=  			{{ com constant }}
  | ( int sz ) INT					::   :: int		{{ com int }}	
  | typ undef						::   :: undef		{{ com undef }}
  | ( typ * ) null					::   :: null		{{ com null }}
  | [ </ constj // j /> ]				::   :: arr             {{ com array }}
  | { </ constj // j /> }				::   :: struct          {{ com struct }}
  | ( const )						:: S :: paren           {{ coq [[ const ]] }}

list_const {{ tex \mathit{\overline{const} } }} :: 'list_const_'	::=  		{{ com const list }}
     	    			     	   					{{ coq (list (const)) }}
  | []   		 				::   :: nil		{{ coq nil }}
  | list_const , const 					::   :: cons		{{ coq ([[const]]::[[list_const]]) }}
  | list_const ; list_const'				::   :: append		{{ coq ([[list_const]]++[[list_const']]) }}
  | ( list_const )					:: S :: paren           {{ coq [[ list_const ]] }}

value :: 'value_'					::=  			{{ com values }}
  | id	 						::   :: id		{{ com id }}
  | const						::   :: const   	{{ com constant }}
  | ( value )						:: S :: paren           {{ coq [[ value ]] }}

arg :: 'arg_'  						::=  			{{ com argument }}
       										{{ coq (prod typ id) }}
  | typ id						::   :: def		{{ coq ([[typ]],[[id]]) }}
  | ( arg )						:: S :: paren		{{ coq [[ arg ]] }} 			

list_arg {{ tex \mathit{\overline{arg} } }} :: 'list_arg_'	::=  		{{ com argument list }}
     	    			     	   					{{ coq (list (typ*id)) }}
  | []   		 				::   :: nil		{{ coq nil }}
  | list_arg , arg 					::   :: cons		{{ coq ([[arg]]::[[list_arg]]) }}
  | list_arg ; list_arg'				::   :: append		{{ coq ([[list_arg]]++[[list_arg']]) }}
  | ( list_arg )					:: S :: paren           {{ coq [[ list_arg ]] }}

param :: 'param_'  					::=  			{{ com argument }}
       										{{ coq (prod typ value) }}
  | typ value						::   :: def		{{ coq ([[typ]],[[value]]) }}
  | ( param )						:: S :: paren		{{ coq [[ param ]] }} 			

list_param {{ tex \mathit{\overline{param} } }} :: 'list_param_'::=		{{ com parameter list }}
       	      			       	       					{{ coq (list (typ*value)) }}
  | []  						::   :: nil		{{ coq nil }}
  | list_param , param           			::   :: cons		{{ coq (([[param]])::[[list_param]]) }}
  | list_param ; list_param'				::   :: append		{{ coq ([[list_param]]++[[list_param']]) }}
  | ( list_param )					:: S :: paren           {{ coq [[ list_param ]] }}

ls {{ tex \mathit{\{l\} } }} :: 'ls_'			::=			{{ com label set }}
       	   			      	 					{{ coq (list l) }}
  | empty						::   :: empty		{{ coq (empty_set l) }}
  | l1 .. ln						::   :: set		{{ coq [[l1 .. ln]] }}
  | ls , l						::   :: cons		{{ coq (set_add eq_dec [[l]] [[ls]]) }}
  | ls ; ls'						::   :: append		{{ coq (set_union eq_dec [[ls]] [[ls']]) }}
  | ( ls )						:: S :: paren		{{ coq [[ ls ]] }}

cond :: 'cond_'						::=			{{ com condition code }}
  | eq							::   :: eq		{{ com eq }}
  | ne							::   :: ne		{{ com ne }}
  | ugt							::   :: ugt		{{ com ugt }}
  | uge							::   :: uge		{{ com uge }}
  | ult							::   :: ult		{{ com ult }}
  | ule							::   :: ule		{{ com ule }}
  | sgt							::   :: sgt		{{ com sgt }}
  | sge							::   :: sge		{{ com sge }}
  | slt							::   :: slt		{{ com slt }}
  | sle							::   :: sle		{{ com sle }}

bop :: 'bop_'						::=  			{{ com binary operators }}
  | add							::   :: add             {{ com add }}
  | lshr						::   :: lshr            {{ com lshr }}
  | and							::   :: and             {{ com and }}
  | or							::   :: or              {{ com or }}

extop :: 'extop_'					::=  			{{ com ext operators }}
  | zext						::   :: z               {{ com zext }}
  | sext						::   :: s               {{ com sext }}

castop :: 'castop_'					::=  			{{ com cast operators }}
  | fptoui 						::   :: fptoui		{{ com fptoui }}
  | fptosi 						::   :: fptosi		{{ com fptosi }}
  | uitofp 						::   :: uitofp		{{ com uitofp }}
  | sitofp 						::   :: sitofp		{{ com sitofp }}
  | ptrtoint						::   :: ptrtoint	{{ com ptrtoint }}
  | inttoptr						::   :: inttoptr	{{ com inttoptr }}
  | bitcast 						::   :: bitcast		{{ com bitcast }}

inbounds :: 'inbounds_'					::=  			{{ com inbounds }} {{ coq bool }}
  | inbounds_yes					::   :: yes		{{ com yes }} {{ coq true }}
  | inbounds_no						::   :: no              {{ com no }} {{ coq false }}

tailc :: 'tailc_'					::=  			{{ com tailcall }} {{ coq bool }}
  | tailc_yes						::   :: yes		{{ com yes }} {{ coq true }}
  | tailc_no						::   :: no              {{ com no }} {{ coq false }}

noret :: 'noret_'					::=  			{{ com noret }} {{ coq bool }}
  | noret_yes						::   :: yes		{{ com yes }} {{ coq true }}
  | noret_no						::   :: no              {{ com no }} {{ coq false }}

cmd :: 'insn_'					    	::=			{{ com commands }}
%% Binary Operations and Bitwise Binary Operations
  | id = bop ( int sz ) value1 value2			::   :: bop		{{ com bop }}
%% Vector Operations
  %| id = extractelement typ0 value0 , const1		::   :: extractelement  {{ com extractelement }}
  %| id = insertelement typ0 value0 , typ1 value1 , const2	::   :: insertelement   {{ com insertelement }}
%% Aggregate Operations
  | id = extractvalue typ value </ constj // j />       ::   :: extractvalue	        {{ com extractvalue }}
  | id = insertvalue typ value typ' value' </ constj // j /> ::   :: insertvalue	{{ com insertvalue }}
%% Memory Access and Addressing Operations
  | id = malloc typ sz align  	         	     	::   :: malloc		{{ com malloc }}
  | id = free ( typ * ) value     	       	     	::   :: free		{{ com free }}
  | id = alloca typ sz align     	       	     	::   :: alloca		{{ com alloca }}
  | id = load ( typ * ) value1				::   :: load		{{ com load }}
  | id = store typ value1 value2			::   :: store		{{ com store }}
  | id = getelementptr inbounds ( typ * ) value </ valuej // j />    	      ::  :: gep        {{ com getelementptr }}
%% Conversion Operations
  %| id = trunc typ1 value1 to typ2			::   :: trunc		{{ com trunc }}
  | id = extop typ1 value to typ2                   	::   :: ext             {{ com ext }}
  | id = castop typ1 value1 to typ2		    	::   :: cast		{{ com cast }}
%% Other Operations
  | id = icmp cond typ value1 value2			::   :: icmp		{{ com icmp }}
  %| id = fcmp cond typ value1 , value2			::   :: fcmp		{{ com fcmp }}
  | id = select value0 typ value1 value2                ::   :: select          {{ com select }}
  | id = noret tailc call typ0 id0 list_param  	 	::   :: call		{{ com call }} 
  | ( insn )						:: S :: paren           {{ coq [[ insn ]] }}

terminator :: 'insn_'					::=  			{{ com termniators }}
  | id = ret typ value        			    	::   :: return      	{{ com return }}
  | id = ret void   					::   :: return_void  	{{ com return void }}
  | id = br value l1 l2		 			::   :: br              {{ com br }}        
  | id = br l        	    	  			::   :: br_uncond       {{ com unconditional br }}        
  %| id = switch typ value , l </ [ constj , lj ] // j />	::   :: switch		{{ com switch }}
  %| id = invoke typ0 id0 list_param to l1 unwind l2 	::   :: invoke		{{ com invoke }} 
  | id = unreachable   	   	     	 		::   :: unreachable	{{ com unreachable }}
  | ( terminator )					:: S :: paren1          {{ coq [[ terminator ]] }}

phinode :: 'insn_'					::=  			{{ com phinodes }}
  | id = phi typ </ [ idj , lj ] // j />		::   :: phi  	    	{{ com def }}
  | ( phinode )						:: S :: paren2          {{ coq [[ phinode ]] }}

insn :: 'insn_'						::=  			{{ com instructions }}
  | phinode						::   :: phinode         {{ com phinodes }}
  | cmd							::   :: cmd		{{ com commands }}
  | terminator						::   :: terminator	{{ com terminator }}

id_labels :: 'id_labels_'				::=  			{{ com id labels }}
	      									{{ coq list (id*l) }}
  | []	      						::   :: nil		{{ coq nil }}
  | id_labels , id l                                    ::   :: cons            {{ coq (([[id]],[[l]])::[[id_labels]]) }}
  | ( id_labels )                                       :: S :: paren           {{ coq [[ id_labels ]] }}

list_insn {{ tex \mathit{\overline{insn} } }} :: 'list_insn_'	::=		{{ com insn list }}
      	     			       	     					{{ coq (list insn) }}
  | []	                  				::   :: nil		{{ coq nil }}
  | list_insn , insn           				::   :: cons		{{ coq ([[insn]]::[[list_insn]]) }}
  | list_insn ; list_insn'				::   :: append		{{ coq ([[list_insn]]++[[list_insn']]) }}
  | ( list_insn )					:: S :: paren           {{ coq [[ list_insn]] }}

list_cmd {{ tex \mathit{\overline{cmd} } }} :: 'list_cmd_'	::=		{{ com cmd list }}
      	     			       	     					{{ coq (list cmd) }}
  | []	                  				::   :: nil		{{ coq nil }}
  | list_cmd , cmd           				::   :: cons		{{ coq ([[cmd]]::[[list_cmd]]) }}
  | list_cmd ; list_cmd'				::   :: append		{{ coq ([[list_cmd]]++[[list_cmd']]) }}
  | ( list_cmd )					:: S :: paren           {{ coq [[ list_cmd ]] }}

list_phinode {{ tex \mathit{\overline{phinode} } }} :: 'list_phinode_'	::=	{{ com phinode list }}
      	     			       	     					{{ coq (list phinode) }}
  | []	                  				::   :: nil		{{ coq nil }}
  | list_phinode , phinode     				::   :: cons		{{ coq ([[phinode]]::[[list_phinode]]) }}
  | list_phinode ; list_phinode'			::   :: append		{{ coq ([[list_phinde]]++[[list_phinode']]) }}
  | ( list_phinode )					:: S :: paren           {{ coq [[ list_phinode ]] }}

block :: 'block_' 					::=			{{ com block }}
  | l list_phinode list_cmd terminator			::   :: intro
  | ( block )						:: S :: paren           {{ coq [[ block ]] }}

opt_block {{ tex \mathit{opt_{block} } }} :: 'opt_block_' ::=			{{ com option block }} {{ coq option block }}
  |  none 						:: M :: none		{{ com none }} {{ coq None }}
  |  block						:: M :: some  		{{ com some }} {{ coq (Some [[block]]) }}
		   
list_block {{ tex \mathit{\overline{block} } }} :: 'list_block_'::=		{{ com block list }}
      	     			       	     					{{ coq (list block) }}
  | []  						::   :: nil		{{ coq nil }}
  | list_block , block					::   :: cons		{{ coq ([[block]]::[[list_block]]) }}
  | list_block ; list_block'				::   :: append		{{ coq ([[list_block]]++[[list_block']]) }}
  | ( list_block )					:: S :: paren           {{ coq [[ list_block ]] }}

fheader :: 'fheader_'    				::=			{{ com function header }}
  | typ id ( list_arg ) 				::   :: intro
  | ( fheader )						:: S :: paren           {{ coq [[ fheader ]] }}

fdec :: 'fdec_'       					::=			{{ com function declaration }}
  | declare fheader     				::   :: intro
  | ( fdec )						:: S :: paren           {{ coq [[ fdec ]] }}

fdef :: 'fdef_'          				::=			{{ com function definition }}
  | define fheader { list_block } 			::   :: intro
  | ( fdef )						:: S :: paren           {{ coq [[ fdef ]] }}

opt_fdec {{ tex \mathit{opt_{fdec} } }} :: 'opt_fdec_'	::=  			{{ com option fdec }} {{ coq option fdec }}
  | none 						::   :: none		{{ coq None }}
  | fdec						::   :: fdec		{{ coq (Some [[fdec]]) }}
  | ( opt_fdec )					:: S :: paren		{{ coq [[ opt_fdec ]] }}

opt_fdef {{ tex \mathit{opt_{fdef} } }} :: 'opt_fdef_'	::=  			{{ com option fdef }} {{ coq option fdef }}
  | none 						::   :: none		{{ coq None }}
  | fdef						::   :: fdec		{{ coq (Some [[fdef]]) }}
  | ( opt_fdef )					:: S :: paren		{{ coq [[ opt_fdef ]] }}

% namedt :: 'namedt_'         				::=			{{ com named type }}
%   | id = type typ   					::   :: intro 
%   | ( namedt )						:: S :: paren		{{ coq [[ namedt ]] }}

% global id is also taken as constant addresses
gvar :: 'gvar_'   	 				::=			{{ com global variables }}
  | id = global typ value align				::   :: intro           
  | ( gvar )	    					:: S :: paren		{{ coq [[ gvar ]] }}

id_binding :: 'id_binding_'  				::=			{{ com id binding }} 
  |  none 						::  :: none		{{ com none }} 
  |  cmd						::  :: cmd  		{{ com cmd }} 
  |  phinode						::  :: phinode		{{ com phinode }}
  |  terminator						::  :: terminator	{{ com terminator }}
  |  gvar						::  :: gvar             {{ com gvar }} 
  |  fdec						::  :: fdec		{{ com fdec }}
  |  arg 						::  :: arg		{{ com arg }}
		   
product  :: 'product_'					::=			{{ com prodoct }}
  | gvar						::   :: gvar     	{{ com global variable }}
  | fdec             					::   :: fdec   		{{ com function declaration }}
  | fdef             					::   :: fdef		{{ com function definition }}
%  | namedt           					::   :: namedtype     	{{ com named types }}

list_product {{ tex \mathit{\overline{product} } }} :: 'list_product_'::=	{{ com list product }}
	    				     	   				{{ coq (list product) }}	   
  | []   						::   :: nil        	{{ com nil }}
    							     			{{ coq nil }}
  | list_product , product				::   :: cons            {{ com cons }}
    	       						     			{{ coq ([[product]]::[[list_product]]) }}
  | ( list_product )					:: S :: paren		{{ coq [[ list_product ]] }}			

module :: 'module_'					::=  			{{ com module }}
  | list_layout list_product 				::   :: intro		{{ com prods }}
  | ( module )					        :: S :: paren           {{ coq [[ module ]] }}

list_module {{ tex \mathit{\overline{module} } }} :: 'list_module_'::=  	{{ com list module }}
      	     			       	     					{{ coq (list module) }}
  | []  						::   :: nil		{{ coq nil }}
  | list_module , module				::   :: cons		{{ coq ([[module]]::[[list_module]]) }}
  | list_module ; list_module'				::   :: append		{{ coq ([[list_module]]++[[list_module']]) }}
  | ( list_module )					:: S :: paren           {{ coq [[ list_module ]] }}

system :: 'system_'					::=  			{{ com system }}
       	  									{{ coq list_module }}
  | list_module						::   :: def		{{ com list of modules }}
    							     			{{ coq [[list_module]] }}

dt :: 'dt_'						::=  			{{ com dominator }}
       										{{ coq (l -> list l) }}

usedef_id :: 'usedef_id_'				::=			{{ com id usedef }}
       	  									{{ coq (id -> (list id)) }} 

usedef_block :: 'usedef_block_'				::=			{{ com label usedef }}
       	  									{{ coq (block -> (list block)) }} 

module_info :: 'module_info_'				::=			{{ com module information }}
	       									{{ coq (prod module (prod usedef_id usedef_block)) }}		
  | module , usedef_id , usedef_block			::   ::	def		{{ com def }} 
   	     		  				     			{{ coq ([[module]], ([[usedef_id]], [[usedef_block]])) }}	
  | ( module_info )					:: S :: paren           {{ coq [[ module_info ]] }}

fdef_info :: 'fdef_info_'				::=  			{{ com fdef information }}
	     									{{ coq (prod fdef dt) }}
  | fdef , dt						::   :: def		{{ com def }}
    	   						     			{{ coq ([[fdef]], [[dt]]) }}
  | ( fdef_info )					:: S :: paren		{{ coq [[ fdef_info ]] }}						

intrinsic_funs :: 'intrinsic_funs_'			::=  			{{ com intrinsic functions }}
	      	 								{{ coq ids }}

terminals :: 'terminals_' ::=
  | =                   ::   :: assignment      {{ tex = }}
  | <->                 ::   :: alias		{{ tex <-> }}
  | == 			::   :: eq		{{ tex == }}
  | |-			::   :: shows           {{ tex \vdash }}
  | |-i			::   :: ishows          {{ tex \vdash_{i} }}
  | |-t			::   :: tshows          {{ tex \vdash_{t} }}
  | |-f			::   :: fshows          {{ tex \vdash_{f} }}
  | in			::   :: in		{{ tex \in }}
  | ;			::   :: colon		{{ tex ; }}

embed 
{{ coq

Tactic Notation "cmd_cases" tactic(first) tactic(c) :=
  first;
  [ c "insn_bop" | c "insn_extractvalue" | c "insn_insertvalue" |
    c "insn_malloc" | c "insn_free" |
    c "insn_alloca" | c "insn_load" | c "insn_store" | c "insn_gep" |
    c "insn_ext" | c "insn_cast" | c "insn_icmp" |  c "insn_select" |
    c "insn_call" ].

End LLVMsyntax.

}}
