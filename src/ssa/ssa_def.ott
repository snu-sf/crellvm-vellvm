
% Syntax of Static Single Assignment(SSA)

embed 
{{ coq
Require Import ListSet.
}}

metavar N ::= {{ com a natural number }}
  {{ coq nat }} {{ lex numeral  }}
metavar id ::= {{ com identities }}
  {{ coq nat }} {{ lex numeral  }}
metavar l ::= {{ com labels }}
  {{ coq nat }} {{ lex numeral  }}
indexvar index, i, j, n ::= {{ com index }}
  {{ coq nat }}
 
grammar

const :: 'const_'					::=  			{{ com constant }}
  | N 	 						::   :: val		{{ com val }}	
  | undef						::   :: undef		{{ com undef }}
  | ( const )						:: S :: paren           {{ coq [[ const ]] }}

value :: 'value_'					::=  			{{ com values }}
  | id	 						::   :: id		{{ com id }}
  | const						::   :: constant	{{ com constant }}
  | ( value )						:: S :: paren           {{ coq [[ value ]] }}

typ :: 'typ_'						::=			{{ com type }}
  | int N                    				::   :: int         	{{ com integer }}
  | float						::   :: float		{{ com float }}
  | double						::   :: double          {{ com double }}
  | fp128						::   :: fp128           {{ com fp128 }}
  | void                      				::   :: void        	{{ com void }}
  | label                                               ::   :: label           {{ com label }}
  | metadata                                            ::   :: metadata        {{ com metadata }}
  | [ N * typ ]               				::   :: array       	{{ com array }}
  | typ ( </ typj // j /> )  				::   :: function    	{{ com function }}
  | { </ typj // j /> }       				::   :: struct      	{{ com structure }}
  | typ *                     				::   :: pointer     	{{ com pointer }}
  | < N * typ >               				::   :: vector       	{{ com vector }}
  | opaque  						::   :: opaque          {{ com opaque }}
  % x86_fp80
  % ppc_fp128
  % union
  | ( typ )						:: S :: paren           {{ coq [[ typ ]] }}


list_arg {{ tex \mathit{\overline{arg} } }} :: 'list_arg_'	::=  			{{ com argument list }}
     	    			     	   					{{ coq (list typ) }}
  | []   		 				::   :: nil		{{ coq nil }}
  | list_arg , typ 					::   :: cons		{{ coq ([[typ]]::[[list_arg]]) }}
  | list_arg ; list_arg'				::   :: append		{{ coq ([[list_arg]]++[[list_arg']]) }}
  | ( list_arg )					:: S :: paren           {{ coq [[ list_arg ]] }}

list_param {{ tex \mathit{\overline{param} } }} :: 'list_param_'::=			{{ com parameter list }}
       	      			       	       					{{ coq (list (typ*value)) }}
  | []  						::   :: nil		{{ coq nil }}
  | list_param , typ value                              ::   :: cons		{{ coq (([[typ]],[[value]])::[[list_param]]) }}
  | list_param ; list_param'				::   :: append		{{ coq ([[list_param]]++[[list_param']]) }}
  | ( list_param )					:: S :: paren           {{ coq [[ list_param ]] }}

ls {{ tex \mathit{\{l\} } }} :: 'ls_'			::=			{{ com label set }}
       	   			      	 					{{ coq (list l) }}
  | empty						::   :: empty		{{ coq (empty_set l) }}
  | l1 .. ln						::   :: set		{{ coq [[l1 .. ln]] }}
  | ls , l						::   :: cons		{{ coq (set_add eq_nat_dec [[l]] [[ls]]) }}
  | ls ; ls'						::   :: append		{{ coq (set_union eq_nat_dec [[ls]] [[ls']]) }}
  | ( ls )						:: S :: paren		{{ coq [[ ls ]] }}

cond :: 'cond_'						::=			{{ com condition code }}
  | eq							::   :: eq		{{ com eq }}
  | ne							::   :: ne		{{ com ne }}
  | ugt							::   :: ugt		{{ com ugt }}
  | uge							::   :: uge		{{ com uge }}
  | ult							::   :: ult		{{ com ult }}
  | ule							::   :: ule		{{ com ule }}
  | sgt							::   :: sgt		{{ com sgt }}
  | sge							::   :: sge		{{ com sge }}
  | slt							::   :: slt		{{ com slt }}
  | sle							::   :: sle		{{ com sle }}

insn :: 'insn_'					    	::=			{{ com insnruction }}
  | ret typ value             			    	::   :: return      	{{ com return }}
  | ret void                   			    	::   :: return_void  	{{ com return void }}
  | br value , l1 , l2					::   :: br              {{ com br }}        
  | br l        	    	  			::   :: br_uncond       {{ com unconditional br }}        
  | switch value , l </ [ constj , lj ] // j />		::   :: switch		{{ com switch }}
  | id = invoke t id0 list_param to l1 unwind l2 	::   :: invoke		{{ com invoke }} 
  | unreachable	     	   	     	 		::   :: unreachable	{{ com unreachable }}
  | id = add typ value1 value2				::   :: add		{{ com add }}
  | id = fadd typ value1 value2				::   :: fadd		{{ com fadd }}
  | id = udiv typ value1 value2				::   :: udiv		{{ com udiv }}
  | id = fdiv typ value1 value2				::   :: fdiv		{{ com fdiv }}
  | id = or typ value1 value2				::   :: or 		{{ com or }}
  | id = and typ value1 value2				::   :: and 		{{ com and }}
  | id = extractelement < N0 * typ0 > id0 , const1	::   :: extractelement  {{ com extractelement }}
  | id = insertelement < N0 * typ0 > id0 , typ1 value1 , const2	    ::   :: insertelement       {{ com insertelement }}
  | id = extractvalue { </ typj // j /> } id0 , const1              ::   :: extractvalue	{{ com extractvalue }}
  | id = insertvalue { </ typj // j /> } id0 , typ1 value1 , const2 ::   :: insertvalue		{{ com insertvalue }}
  | id = alloca typ N  	     	       	     	       	::   :: alloca		{{ com alloca }}
  | id = load typ1 id1					::   :: load		{{ com load }}
  | store typ1 value1 , typ2 id2			::   :: store		{{ com store }}
  | id = trunc typ1 value1 to typ2			::   :: trunc		{{ com trunc }}
  | id = fptrunc typ1 value1 to typ2			::   :: fptrunc		{{ com fptrunc }}
  | id = fptoui typ1 value1 to typ2			::   :: fptoui		{{ com fptoui }}
  | id = fptosi typ1 value1 to typ2			::   :: fptosi		{{ com fptosi }}
  | id = uitofp typ1 value1 to typ2			::   :: uitofp		{{ com uitofp }}
  | id = sitofp typ1 value1 to typ2			::   :: sitofp		{{ com sitofp }}
  | id = ptrtoint typ1 value1 to typ2			::   :: ptrtoint	{{ com ptrtoint }}
  | id = inttoptr typ1 value1 to typ2			::   :: inttoptr	{{ com inttoptr }}
  | id = bitcast typ1 value1 to typ2			::   :: bitcase		{{ com bitcast }}
  | id = icmp cond typ value1 , value2			::   :: icmp		{{ com icmp }}
  | id = fcmp cond typ value1 , value2			::   :: fcmp		{{ com fcmp }}
  | id = phi typ </ [ idj , lj ] // j />		::   :: phi  	    	{{ com phi }}
  | ( insn )						:: S :: paren           {{ coq [[ insn ]] }}

opt_insn {{ tex \mathit{opt_{insn} } }} :: 'opt_insn_'  ::=			{{ com option insn }} {{ coq option insn }}
  |  none 						:: M :: none		{{ com none }} {{ coq None }}
  |  insn						:: M :: some  		{{ com some }} {{ coq (Some [[insn]]) }}
		   
list_insn {{ tex \mathit{\overline{insn} } }} :: 'list_insn_'	::=			{{ com insnruction list }}
      	     			       	     					{{ coq (list insn) }}
  | []	                  				::   :: nil		{{ coq nil }}
  | list_insn , insn           				::   :: cons		{{ coq ([[insn]]::[[list_insn]]) }}
  | list_insn ; list_insn'				::   :: append		{{ coq ([[list_insn]]++[[list_insn']]) }}
  | ( list_insn )					:: S :: paren           {{ coq [[ list_insn ]] }}

block :: 'block_' 					::=			{{ com block }}
  | l list_insn						::   :: def
  | ( block )						:: S :: paren           {{ coq [[ block ]] }}

opt_block {{ tex \mathit{opt_{block} } }} :: 'opt_block_' ::=			{{ com option block }} {{ coq option block }}
  |  none 						:: M :: none		{{ com none }} {{ coq None }}
  |  block						:: M :: some  		{{ com some }} {{ coq (Some [[block]]) }}
		   
list_block {{ tex \mathit{\overline{block} } }} :: 'list_block_'::=			{{ com block list }}
      	     			       	     					{{ coq (list block) }}
  | []  						::   :: nil		{{ coq nil }}
  | list_block , block					::   :: cons		{{ coq ([[block]]::[[list_block]]) }}
  | list_block ; list_block'				::   :: append		{{ coq ([[list_block]]++[[list_block']]) }}
  | ( list_block )					:: S :: paren           {{ coq [[ list_block ]] }}

fheader :: 'fheader_'    				::=			{{ com function header }}
  | typ ident ( list_arg ) 				::   :: intro
  | ( fheader )						:: S :: paren           {{ coq [[ fheader ]] }}

fdec :: 'fdec_'       					::=			{{ com function declaration }}
  | declare fheader     				::  :: intro
  | ( fdec )						:: S :: paren           {{ coq [[ fdec ]] }}

fdef :: 'fdef_'          				::=			{{ com function definition }}
  | define fheader { list_block } 			::  :: intro
  | ( fdef )						:: S :: paren           {{ coq [[ fdef ]] }}

namedt :: 'namedt_'         				::=			{{ com named type }}
  | id = type typ   					::   :: intro 

g :: 'g_'   	 					::=			{{ com global }}
  | id = global typ const				::   :: intro  

module :: 'module_'    	    				::=			{{ com module }}
  | nil   						::   :: nil        	{{ com nil }}
  | module , g						::   :: global_var      {{ com global variable }}
  | module , fdec             				::   :: function_dec   	{{ com function declaration }}
  | module , fdef             				::   :: function_def   	{{ com function definition }}
  | module , namedt           				::   :: namedtype     	{{ com named types }}

dt :: 'dt_'						::=  			{{ com dominator }}
       										{{ coq (list (prod l ls)) }}

terminals :: 'terminals_' ::=
  | =                   ::   :: assignment      {{ tex = }}
  | == 			::   :: eq		{{ tex == }}
  | |-			::   :: shows           {{ tex \vdash }}

formula :: 'formula_' ::=
  | judgement			::   :: judgement
  | formula1 ... formulan     	::   :: dots
  | not formula                 :: M :: not
      				{{ tex \neg [[formula]] }}
				{{ coq (not ([[formula]])) }}
  | ( formula )                 :: M :: brackets
      	      			{{ tex ([[formula]]\!) }} 
      				{{ coq ([[ formula ]]) }}
  | exists u . formula          ::   :: exists 
      	       			{{ tex \exists [[u]]. [[formula]] }}
      				{{ coq ( exists [[ u ]] , [[ formula ]] ) }}
  | formula /\ formula'         :: M :: and
               			{{ tex [[formula]] \wedge [[formula']] }}
	  			{{ coq [[formula]] /\ [[ formula' ]] }}
  | foreach C . formula         :: M :: forall 
              			{{ tex \forall [[C]]. [[ formula ]] }}
	  			{{ coq ( forall [[C]], [[ formula ]] ) }}
  | formula implies formula'    :: M :: implies
            	    		{{ tex [[formula]] \mathit{implies} [[formula']] }}
	  			{{ coq [[formula]] -> [[ formula' ]] }}
  | formula \/ formula'         :: M :: or
               			{{ tex [[formula]] \vee [[formula']] }}
	  			{{ coq [[formula]] \/ [[ formula' ]] }} 
  | N1 == N2 			::   :: eqN
				{{ tex [[N1]] == [[N2]] }}
				{{ coq [[N1]] = [[N2]] }}
  | l1 == l2 			::   :: eqLabel
				{{ tex [[l1]] == [[l2]] }}
				{{ coq [[l1]] = [[l2]] }}

parsing
formula_and left formula_and

 